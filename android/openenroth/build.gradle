def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}

android {
    /*signingConfigs {
        debug {
            storeFile file(OPENENROTH_RELEASE_STORE_FILE)
            storePassword OPENENROTH_RELEASE_PASSWORD
            keyAlias OPENENROTH_RELEASE_KEY_ALIAS
            keyPassword OPENENROTH_RELEASE_PASSWORD
        }
        release {
            storeFile file(OPENENROTH_RELEASE_STORE_FILE)
            storePassword OPENENROTH_RELEASE_PASSWORD
            keyAlias OPENENROTH_RELEASE_KEY_ALIAS
            keyPassword OPENENROTH_RELEASE_PASSWORD
        }
    }*/
    compileSdkVersion 31
    ndkVersion "25.1.8937393"
    defaultConfig {
        if (buildAsApplication) {
            applicationId "org.openenroth.game"
        }
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_APP_PLATFORM=android-16", "-DANDROID_STL=c++_static"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                version "3.22.1"
            }
        }
        /*signingConfig signingConfigs.release*/
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
    namespace 'org.openenroth.game'
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path 'jni/CMakeLists.txt'
                version "3.22.1"
            }
        }
    }
    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.openenroth.game.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
